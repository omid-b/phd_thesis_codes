#!/bin/csh

cd `dirname $0`
source ../param.csh

clear
cd $homedir

#Study region:
set numNodes = `awk 'NR==2' $grid`
set lon_l1 = `awk 'NR>2' $grid|head -n$numNodes|awk '{print $2}'|sort -n  |awk 'NR==1'`
set lon_l2 = `awk 'NR>2' $grid|head -n$numNodes|awk '{print $2}'|sort -nr |awk 'NR==1'`
set lat_l1 = `awk 'NR>2' $grid|head -n$numNodes|awk '{print $1}'|sort -n  |awk 'NR==1'`
set lat_l2 = `awk 'NR>2' $grid|head -n$numNodes|awk '{print $1}'|sort -nr  |awk 'NR==1'`
set center_lon = `echo "$lon_l1 $lon_l2"|awk '{printf "%.2f\n", ($2 - $1)/2 +$1}'`
set center_lat = `echo "$lat_l1 $lat_l2"|awk '{printf "%.2f\n", ($2 - $1)/2 +$1}'`
set reg = $lon_l1/$lon_l2/$lat_l1/$lat_l2
set prj = "L$center_lon/$center_lat/$lat_l1/$lat_l2/12i"

printf "Center of study region (lon&lat): $center_lon  $center_lat\n\n"


if (-d GCpaths) rm -rf GCpaths
mkdir GCpaths
set num_evt = `ls|grep "[0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9]"|wc|awk '{print $1}'`

@ i=1
echo "Making GCpath files at different periods"
foreach evt (`ls|grep "[0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9]"`)
  cd $evt
  printf "  Working on event $i of $num_evt     \r"
  set evt_lat = `cat $evtloc|grep $evt|awk '{print $3}'`
  set evt_lon = `cat $evtloc|grep $evt|awk '{print $2}'`
  foreach bp (`awk 'NR>1' $passbands|awk '{print $1}'`)
   foreach data (`ls|grep $bp|grep -v gz|grep -v p$`)
     set sta = `echo $data|awk -F"." '{print $3}'`
     set sta_lat = `cat $staloc|grep $sta|awk '{print $3}'`
     set sta_lon = `cat $staloc|grep $sta|awk '{print $2}'`
     echo $sta_lon $sta_lat $evt_lon $evt_lat >> $homedir/GCpaths/$bp.xyxy
     echo $sta_lon $sta_lat $evt_lon $evt_lat >> $homedir/GCpaths/all.xyxy
   end
  end 
  @ i++
  cd $homedir
end
printf "   Working on event $num_evt of $num_evt...Done!   \n\n"


foreach bp (`awk 'NR>1' $passbands|awk '{print $1}'`)
  set OUT = $homedir/GCpaths/$bp.ps
  gmt set FONT_ANNOT_PRIMARY 10p,Helvetica,black FONT_ANNOT_SECONDARY 8p,Helvetica,black \
        FONT_LABEL 12p,Helvetica,black FONT_TITLE 18p,Helvetica-Bold,black FORMAT_GEO_MAP \
        ddd:mm:ssF GMT_VERBOSE n MAP_ANNOT_OBLIQUE 6 MAP_ANNOT_OFFSET 5p MAP_FRAME_PEN \
        thin,black MAP_FRAME_TYPE plain MAP_GRID_CROSS_SIZE_PRIMARY 2 PS_MEDIA $mediaSize \
       MAP_TITLE_OFFSET 0.2i

  gmt psbasemap -R$reg -J$prj -Ba -Bwsen -K > $OUT
  @ i=1
  set numLine = `wc -l $homedir/GCpaths/$bp.xyxy|awk '{print $1}'`
  while ($i <= $numLine)
    printf "   Plotting GC paths: $bp ($i of $numLine)                \r" 
    awk NR==$i $homedir/GCpaths/$bp.xyxy|awk '{printf "%f %f\n%f %f\n",$1,$2,$3,$4}'|psxy -R$reg -J$prj -W0.5p -O -K -P >> $OUT
    @ i++
  end
  printf "   Plotting GC paths ........... Done!           \n\n"
  
  gmt pscoast -R$reg -J$prj -Di -Wthin,red -A15000 -K -O  >> $OUT
#  awk '{printf "%11.6f %11.6f\n",$1,$2}' $homedir/mask.xy|gmt psxy -R$reg -J$prj -Sc0.1i -Gred -O -K >> $OUT
  awk '{printf "%11.6f %11.6f\n",$2,$3}' $staloc|gmt psxy -R$reg -J$prj -Si0.15i -Ggold -Wthick,brown -O >> $OUT
end


