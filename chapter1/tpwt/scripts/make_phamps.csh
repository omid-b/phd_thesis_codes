#!/bin/csh
#This script is a modified version of 'mk_phamps.csh' (by Yang).
#Usage: First, please modify 'Adjustable Parameters' section:
#       $homedir: Full path to processed dataset containing events, passbands...
#       $rdsetupsimul: Full path to compiled 'rdsetupsimul.yang.f'
#UPDATE: Oct 18, 2018

cd `dirname $0`
source ../param.csh
#----Adjustable Parameters----#
set rdsetupsimul = $softwaredir/bin/setupsimul.yy
#-----------------------------#
clear
printf "This script automates calculating phase and amplitude from the input surface wave data contained in the filelists generated by 'make_filelists.csh'.\n"

cd $homedir

if (! -e $rdsetupsimul) then
  echo "\nCould not find rdsetupsimul in $softwaredir/bin/ ! \n"
  exit
endif

set numFilelist = `ls filelists|grep filelists|wc -l`

if ($numFilelist > 0) then
 echo "\n Number of filelists: $numFilelist\n"
else
 echo "\nCould not find 'filelists_*' in dataset directory! EXIT script.\n"
 exit
endif

printf "Do you want to continue (Y/N)? "
set uans = $< 
if ($uans == 'yes' ||$uans == 'YES' ||$uans == 'Y' ||$uans == 'y') then
 echo ""
else
 echo "\n\n Wrong input! EXIT script.\n"
 exit
endif

if (-d phamps) then
  rm -rf phamps
  mkdir phamps
else
  mkdir phamps
endif

ls filelists|grep filelists_ > filelists.list
if (-e bad_headdata) rm -f bad_headdata

foreach list (`cat filelists.list`)
  echo "\n\n working on $list"

  foreach fn (`ls filelists/$list|grep filelist`)

    set per = `echo $fn|awk -F".p" '{print $2}'`
    printf "  Making Phase & Amp for $list/$fn\r"
    if (-e $homedir/phamp_p$per) rm -f $homedir/phamp_p$per
    $rdsetupsimul < filelists/$list/$fn > $homedir/phamp_p$per.log
    rm -f $homedir/phamp_p$per.log #to see log, comment this line!
    if (-e bad_headdat.dat) then
      if (`cat bad_headdat.dat|wc -l` > 0) then
         cat bad_headdat.dat >> bad_headdata
      endif
    endif
  end
  set listName = `echo $list|cut -d"_" -f2-999`
  if (-d phamps/phamps_$listName) rm -rf phamps/phamps_$listName
  mkdir phamps/phamps_$listName
  mv phamp_p??? phamps/phamps_$listName

end

rm -f filelists.list
if (-e bad_headdata) mv bad_headdata bad_headdat.dat
printf '\n\ncomplete! \n'


